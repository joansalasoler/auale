# $Id$
# Maintainer: Joan Sala Soler <contact@joansala.com>
# Contributor: Andrew Dabrowski <dabrowsa@indiana.edu>

pkgname=auale
pkgver=1.1.0
pkgrel=1
pkgdesc="A free mancala game for the serious player"
arch=('any')
url="http://www.joansala.com/auale"
license=('GPL3')
groups=()
depends=(
  'python>=2.6'
  'gtk3>=3.10'
  'java-runtime=7'
  'python-gobject'
  'desktop-file-utils'
)
optdepends=(
  'sdl2: sound support'
  'sdl2_mixer: sound support'
)
makedepends=(
  'python-setuptools'
)
replaces=('python2-auale')
install=${pkgname}.install
source=("http://www.joansala.com/auale/packages/$pkgname-$pkgver.tar.gz")
sha256sums=('44bf86dfcf14dde5be14254ed0697b54eb91d24613d04b3e43c40f514ef87870')

bindir='/usr/bin'
datadir='/usr/share'
mandir='/usr/share/man'
iconsdir='/usr/share/icons'

build() {
    pushd ${pkgname}-${pkgver}/src/${pkgname}
    python setup.py build
    popd
}

package() {
    pushd ${pkgname}-${pkgver}/src/${pkgname}
    python setup.py install \
        --root=${pkgdir} \
        --install-lib=${pkgdir}${datadir}/${pkgname} \
        --skip-build
    popd
    
    # Link the game launcher
    install -d ${pkgdir}${bindir}
    ln -s ${pkgdir}${datadir}/${pkgname}/__main__.py \
          ${pkgdir}${bindir}/${pkgname}
    chmod +x ${pkgdir}${datadir}/${pkgname}/__main__.py
    
    # Install dist files
    pushd ${pkgname}-${pkgver}/res/share
    install -D -m644 applications/${pkgname}.desktop \
        ${pkgdir}${datadir}/applications/${pkgname}.desktop
    install -D -m644 glib-2.0/schemas/com.joansala.${pkgname}.gschema.xml \
        ${pkgdir}${datadir}/glib-2.0/schemas/com.joansala.${pkgname}.gschema.xml
    install -D -m644 man/man6/${pkgname}.6 \
        ${pkgdir}${mandir}/man6/${pkgname}.6
    install -D -m644 mime/packages/${pkgname}.xml \
        ${pkgdir}${datadir}/mime/packages/${pkgname}.xml
    install -D -m644 icons/hicolor/scalable/mimetypes/text-x-oware-ogn.svg \
        ${pkgdir}${iconsdir}/hicolor/scalable/mimetypes/text-x-oware-ogn.svg
    install -D -m644 icons/hicolor/scalable/apps/${pkgname}.svg \
        ${pkgdir}${iconsdir}/hicolor/scalable/apps/${pkgname}.svg
    popd
    
    # Move installed locales
    pushd ${pkgdir}${datadir}/${pkgname}/res
    mv -v messages ${pkgdir}${datadir}/locale
    popd
}